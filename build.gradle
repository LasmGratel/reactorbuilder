buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath("com.anatawa12.forge:ForgeGradle:2.3-1.0.+") {
            changing = true
        }
    }
}

plugins {
	id "com.github.johnrengelman.shadow" version "4.0.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "${mc_version}-${mod_version}"
group = "sonar.reactorbuilder"
archivesBaseName = "NC-ReactorBuilder"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs 'libs'
    }
}

tasks.build.dependsOn shadowJar

shadowJar {
    classifier = ""
    dependencies {
        include(dependency('simplelibrary:SimpleLibraryPlus-1.3'))
    }
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

dependencies {
    compileOnly "curse.maven:nuclearcraft-overhauled-336895:3386487"
    runtimeOnly "curse.maven:nuclearcraft-overhauled-336895:3386487"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.WARN
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
